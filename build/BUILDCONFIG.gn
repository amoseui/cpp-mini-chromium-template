if (target_os == "") {
  target_os = host_os
}

if (current_os == "") {
  current_os = target_os
}

if (target_cpu == "") {
  target_cpu = host_cpu
}

if (current_cpu == "") {
  current_cpu = target_cpu
}

set_default_toolchain(
     "//third_party/mini_chromium/src/build:gcc_like_toolchain")

declare_args() {
  is_debug = true
}

_default_configs = [
  "//third_party/mini_chromium/src/build:default",
  "//third_party/mini_chromium/src/build:Wexit_time_destructors",
]

_default_executable_configs =
    _default_configs + [
      "//third_party/mini_chromium/src/build:executable",
    ]

set_defaults("source_set") {
  configs = _default_configs
}

set_defaults("static_library") {
  configs = _default_configs
}

set_defaults("executable") {
  configs = _default_executable_configs
}

set_defaults("loadable_module") {
  configs = _default_configs
}

set_defaults("shared_library") {
  configs = _default_configs
}

set_defaults("test") {
  configs = _default_executable_configs
}
